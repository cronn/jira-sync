buildscript {
	ext {
		springBootVersion = '1.5.14.RELEASE'
	}
	repositories {
		jcenter()
	}
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
		classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.1'
	}
}

plugins {
	id 'java'
	id 'jacoco'
	id 'eclipse'
	id 'idea'

	id 'com.github.kt3k.coveralls' version '2.8.2'
}

apply plugin: 'org.sonarqube'
apply plugin: 'org.springframework.boot'

group = 'de.cronn'

if (project.hasProperty('projectVersion')) {
	version = projectVersion
} else if (System.env.BUILD_NUMBER) {
	version = "1.0-SNAPSHOT-b" + System.env.BUILD_NUMBER
} else {
	version = "1.0"
}

jacocoTestReport {
	reports {
		html.enabled = false
		xml.enabled = true
		csv.enabled = false
	}
	dependsOn test
}

springBoot {
	executable = true
}

bootRun {
	systemProperties = System.properties
}

eclipse.classpath {
	downloadSources = true
}

idea.module {
	downloadJavadoc = true
	downloadSources = true
}

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
	repositories {
		jcenter()
	}
}

dependencies {
	compile 'javax.annotation:javax.annotation-api:1.2'

	compile('org.springframework.boot:spring-boot-starter')
	compile('org.springframework.boot:spring-boot-starter-cache')
	compile('javax.cache:cache-api')
	compile('org.ehcache:ehcache')

	compile 'org.apache.httpcomponents:httpclient:4.5.6'
	compile 'org.apache.commons:commons-lang3:3.7'

	compile("org.springframework:spring-web")
	compile("com.fasterxml.jackson.core:jackson-databind")
	compile("com.fasterxml.jackson.datatype:jackson-datatype-jsr310")

	compile("de.cronn:reflection-util:2.0.2")
	compile("de.cronn:ssh-proxy:1.3")

	testCompile("org.mockito:mockito-core:2.20.1")
	testCompile("org.assertj:assertj-core:3.10.0")
	testCompile("nl.jqno.equalsverifier:equalsverifier:2.5")
	testCompile('org.springframework.boot:spring-boot-starter-test')
	testCompile("org.springframework.boot:spring-boot-starter-web")
}

wrapper {
	gradleVersion = "5.2.1"
	distributionType = Wrapper.DistributionType.ALL
}
